--  Behavior Annex 2.11-18082009
--  Annex X.3 Behavior Specification
--  Example 1

--  $Id: ba_example_001.aadl 131 2009-10-28 12:52:35Z lasnier $

-- This first example shows the use of variable labels as local variables to 
-- pass data between successive actions.

package example1 
public

data number
end number;

subprogram mul
features
  x : in parameter number;
  y : in parameter number;
  z : out parameter number;
end mul;

subprogram cube
features
  x : in parameter number;
  y : out parameter number;
  mul : requires subprogram access mul;
end cube;

subprogram implementation cube.ba
annex behavior_specification {**
  variables tmp : number;
  states s : initial final state;
  transitions t : s -[]-> s { mul!(x,x,tmp); mul!(tmp,x,y) };
**};
end cube.ba;
-- Such a basic use of the behavior annex simply corresponds to a shortcut for an equivalent calls 
-- specification in core AADL:

subprogram implementation cube.no_ba
calls t : {
  mul1 : subprogram mul;
  mul2 : subprogram mul;
};
connections
  parameter x -> mul1.x;
  parameter x -> mul1.y;
  parameter mul1.z -> mul2.x;
  parameter x -> mul2.y;
  parameter mul2.z -> y;
end cube.no_ba;

end example1;
